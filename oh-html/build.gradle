plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'org.apache.commons:commons-text:1.14.0'
    implementation 'com.github.template72:template72:1.0.0'
    implementation 'org.tinylog:tinylog:1.3.6'

    testImplementation 'junit:junit:4.12'
}

// gradlew.bat -Pgroup= -Pversion= -Pownrepo= -Prepourl= -Prepouser= -Prepopass=  publish

repositories {
    def ownrepo = findProperty('ownrepo')
    if (ownrepo != null && ownrepo != '') {
        maven {
            url = ownrepo
            allowInsecureProtocol = true
        }
    } else {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        artifactory(MavenPublication) {
            from components.java
        }
    }
 
    repositories {
        maven {
            url = findProperty('repourl')
            allowInsecureProtocol = true
            credentials {
                username findProperty('repouser')
                password findProperty('repopass')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}
